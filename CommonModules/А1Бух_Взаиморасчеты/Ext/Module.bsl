#Если НЕ Клиент Тогда
	
	// Перепроводит документы расчетов с контрагентами до даты, восстанавливая правильные проводки.
	// Обрабатывает только документы с движениями по 60 и 62 счету.
	//
	// Параметры:
	//  Контрагенты		 - Массив,Неопределено - отбор по контрагентам. Если Неопределено, обрабатываются все контрагенты. 
	//  ДатаОкончания	 - Дата,Неопределено - дата. Если Неопределено, восстанавливает до текущей даты.
	// 
	// Возвращаемое значение:
	//   - 
	//
	Функция Восстановить(Знач Контрагенты = Неопределено, Знач ДатаОкончания = Неопределено, Знач ПропускатьОшибки = Ложь) Экспорт
		Если ДатаОкончания = Неопределено Тогда ДатаОкончания = ТекущаяДата() КонецЕсли;
		МассивЧастей = Новый Массив;
		МассивЧастей.Добавить(ТекстЗапросаПервыеНеактуальности(Контрагенты));
		МассивЧастей.Добавить(
		"ВЫБРАТЬ
		|	Хозрасчетный.Период КАК Период,
		|	Хозрасчетный.Регистратор КАК Ссылка
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(, &ДатаОкончания, Регистратор, Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками)), , , , ) КАК Хозрасчетный
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПервыеНеактуальности КАК ПервыеНеактуальности
		|		ПО Хозрасчетный.Субконто1 = ПервыеНеактуальности.Контрагент
		|			И Хозрасчетный.Период >= ПервыеНеактуальности.Дата
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период,
		|	Ссылка");
		Запрос = Новый Запрос(А1Э_Запросы.Соединить(МассивЧастей));
		Запрос.УстановитьПараметр("Контрагенты", Контрагенты);
		Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Попытка
				НачатьТранзакцию();
				ДокОб = Выборка.Ссылка.ПолучитьОбъект();
				ДокОб.Записать(РежимЗаписиДокумента.Проведение);
				НаборЗаписей = Последовательности.ДокументыОрганизаций.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(ДокОб.Ссылка);
				Запись = НаборЗаписей.Добавить();
				Запись.Период = ДокОб.Дата;
				Запись.Регистратор = ДокОб.Ссылка;
				Запись.Организация = ДокОб.Организация;
				Запись.СостояниеПроведения = Перечисления.СостоянияПроведенияВПоследовательности.ПроведенВПоследовательности;
				НаборЗаписей.Записать(Истина);
				ЗафиксироватьТранзакцию();
			Исключение
				Если ПропускатьОшибки Тогда
					ОтменитьТранзакцию();
					Сообщить("Не удалось провести документ " + ДокОб.Ссылка + "! Данные могут быть неверны! Обратитесь к заведующему учетом.");
				Иначе
					А1Э_Служебный.СлужебноеИсключение("Ошибка проведения документа " + ДокОб.Ссылка + " в ходе восстановления последовательности расчетов!");
				КонецЕсли;
			КонецПопытки;
		КонецЦикла;
	КонецФункции
	
	Функция ДатаАктуальности(Знач Контрагенты = Неопределено) Экспорт
		МассивЧастей = Новый Массив;
		МассивЧастей.Добавить(ТекстЗапросаПервыеНеактуальности(Контрагенты));
		МассивЧастей.Добавить(
		"ВЫБРАТЬ
		|	МИНИМУМ(ПервыеНеактуальности.Дата) КАК Дата
		|ИЗ
		|	ПервыеНеактуальности КАК ПервыеНеактуальности");
		Запрос = Новый Запрос(А1Э_Запросы.Соединить(МассивЧастей));
		Запрос.УстановитьПараметр("Контрагенты", Контрагенты);
		Запрос.УстановитьПараметр("ДатаОкончания", ТекущаяДата());
		Возврат А1Э_Запросы.ПервыйРезультат(Запрос);
	КонецФункции
	
	Функция ТекстЗапросаПервыеНеактуальности(Контрагенты = Неопределено) Экспорт
		Текст = 
		"ВЫБРАТЬ
		|	МИНИМУМ(Последовательность.Период) КАК Дата,
		|	ВЫРАЗИТЬ(Хозрасчетный.Субконто1 КАК Справочник.Контрагенты) КАК Контрагент
		|ПОМЕСТИТЬ ПервыеНеактуальности
		|ИЗ
		|	Последовательность.ДокументыОрганизаций КАК Последовательность
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Обороты(, &ДатаОкончания, Регистратор, Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками)), , , , ) КАК Хозрасчетный
		|		ПО Последовательность.Регистратор = Хозрасчетный.Регистратор
		|ГДЕ
		|	Последовательность.СостояниеПроведения = ЗНАЧЕНИЕ(Перечисление.СостоянияПроведенияВПоследовательности.ПроведенСНарушениемПоследовательности)
		|	И &УсловиеКонтрагенты
		|
		|СГРУППИРОВАТЬ ПО
		|	Последовательность.Организация,
		|	ВЫРАЗИТЬ(Хозрасчетный.Субконто1 КАК Справочник.Контрагенты)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент";
		Если Контрагенты = Неопределено Тогда
			А1Э_Строки.Подставить(Текст, "&УсловиеКонтрагенты", "ИСТИНА");
		Иначе
			Контрагенты = А1Э_Массивы.Массив(Контрагенты);
			А1Э_Строки.Подставить(Текст, "&УсловиеКонтрагенты", "Хозрасчетный.Субконто1 В (&Контрагенты)");
		КонецЕсли;
		Возврат Текст;
	КонецФункции 
#КонецЕсли

