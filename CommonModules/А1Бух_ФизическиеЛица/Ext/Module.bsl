#Если НЕ Клиент Тогда
	// Возвращает соответствие "Физлицо"->"Структура паспортных данных". 
	// Если физическое лицо передано ссылкой, то возвращает только структуру.
	// Анализирует только паспорта РФ (ТУДУ?).
	//
	// Параметры:
	//  ФизическиеЛица	 - СправочникСсылка.ФизическиеЛица,Массив - 
	//  Дата			 - Дата,Неопределено - дата, на которую будут получены сведения. По умолчанию - текущая дата. 
	// 
	// Возвращаемое значение:
	//   - Соответствие, Структура 
	//
	Функция ПаспортныеДанные(ФизическиеЛица, Знач Дата = Неопределено) Экспорт
		Если Дата = Неопределено Тогда Дата = ТекущаяДата(); КонецЕсли;
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДокументыФизическихЛицСрезПоследних.Физлицо КАК Физлицо,
		|	ДокументыФизическихЛицСрезПоследних.ВидДокумента КАК ВидДокумента,
		|	ДокументыФизическихЛицСрезПоследних.Серия КАК Серия,
		|	ДокументыФизическихЛицСрезПоследних.Номер КАК Номер,
		|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи КАК ДатаВыдачи,
		|	ДокументыФизическихЛицСрезПоследних.СрокДействия КАК СрокДействия,
		|	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК КемВыдан,
		|	ДокументыФизическихЛицСрезПоследних.КодПодразделения КАК КодПодразделения,
		|	ДокументыФизическихЛицСрезПоследних.ЯвляетсяДокументомУдостоверяющимЛичность КАК ЯвляетсяДокументомУдостоверяющимЛичность,
		|	ДокументыФизическихЛицСрезПоследних.Представление КАК Представление,
		|	ДокументыФизическихЛицСрезПоследних.ФамилияЛатиницей КАК ФамилияЛатиницей,
		|	ДокументыФизическихЛицСрезПоследних.ИмяЛатиницей КАК ИмяЛатиницей,
		|	ДокументыФизическихЛицСрезПоследних.СтранаВыдачи КАК СтранаВыдачи
		|ИЗ
		|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(
		|			&Дата,
		|			ФизЛицо В (&ФизическиеЛица)
		|				И ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ПаспортРФ)) КАК ДокументыФизическихЛицСрезПоследних";
		Запрос.УстановитьПараметр("Дата", Дата);
		Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
		Выборка = Запрос.Выполнить().Выбрать();
		Соответствие = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			ПаспортныеДанные = Новый Структура("ФизЛицо,Серия,Номер,ДатаВыдачи,СрокДействия,КемВыдан,КодПодразделения,СтранаВыдачи"); 
			ЗаполнитьЗначенияСвойств(ПаспортныеДанные, Выборка);
			Соответствие.Вставить(Выборка.Физлицо, ПаспортныеДанные);
		КонецЦикла;
		Если ТипЗнч(ФизическиеЛица) = Тип("Массив") Тогда
			Возврат Соответствие;
		Иначе
			Возврат Соответствие[ФизическиеЛица];
		КонецЕсли;
	КонецФункции 
#КонецЕсли
 