#Область Механизм

Функция НастройкиМеханизма() Экспорт
	Настройки = А1Э_Механизмы.НовыйНастройкиМеханизма();
	
	Настройки.Обработчики.Вставить("ПриЗаписи", Истина);
	
	Возврат Настройки;
КонецФункции

Функция ПриЗаписи(Объект, Отказ) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	УниверсальныйРегистратор = ПолучитьСуществующий(Объект.Ссылка);
	
	Если НЕ Объект.ПометкаУдаления Тогда
		Если НЕ ЗначениеЗаполнено(УниверсальныйРегистратор) Тогда Возврат Неопределено; КонецЕсли;
	
		УниверсальныйРегистраторОбъект = УниверсальныйРегистратор.ПолучитьОбъект();
		УниверсальныйРегистраторОбъект.ОбменДанными.Загрузка = Истина;
		Заполнить(УниверсальныйРегистраторОбъект, Объект);
		УниверсальныйРегистраторОбъект.Записать(РежимЗаписиДокумента.Запись);
	Иначе
		Если НЕ ЗначениеЗаполнено(УниверсальныйРегистратор) Тогда Возврат Неопределено; КонецЕсли;
		
		УниверсальныйРегистраторОбъект = УниверсальныйРегистратор.ПолучитьОбъект();
		УниверсальныйРегистраторОбъект.Удалить();
	КонецЕсли;
КонецФункции

#КонецОбласти 

Функция ПолучитьСуществующий(Ссылка) Экспорт
	Возврат Документы.ОперацияБух.НайтиПоРеквизиту("А1Бух_Основание", А1Э_Метаданные.ИдентификаторПоСсылке(Ссылка));
КонецФункции

Функция Создать(ИсточникДанных) Экспорт
	УниверсальныйРегистраторОбъект = Документы.ОперацияБух.СоздатьДокумент();
	УниверсальныйРегистраторОбъект.ОбменДанными.Загрузка = Истина;
	Заполнить(УниверсальныйРегистраторОбъект, ИсточникДанных);
	УниверсальныйРегистраторОбъект.Записать(РежимЗаписиДокумента.Запись);
	Возврат УниверсальныйРегистраторОбъект; 
КонецФункции

Функция Заполнить(УниверсальныйРегистраторОбъект, ИсточникДанных) 
	Если А1Э_Общее.Свойство(ИсточникДанных, "Организация") Тогда
		УниверсальныйРегистраторОбъект.Организация = ИсточникДанных.Организация;
	КонецЕсли;
	УниверсальныйРегистраторОбъект.Дата = ИсточникДанных.Дата;
	УниверсальныйРегистраторОбъект.А1Бух_Основание = А1Э_Метаданные.ИдентификаторПоСсылке(ИсточникДанных.Ссылка);
	УниверсальныйРегистраторОбъект.А1Бух_Представление = "" + ИсточникДанных.Ссылка;
КонецФункции

Функция Получить(ИсточникДанных) Экспорт
	УниверсальныйРегистратор = ПолучитьСуществующий(ИсточникДанных.Ссылка);
	Если ЗначениеЗаполнено(УниверсальныйРегистратор) Тогда
		Возврат УниверсальныйРегистратор;
	КонецЕсли;
	Возврат Создать(ИсточникДанных).Ссылка;
КонецФункции 