#Если НЕ Клиент Тогда
	
	Функция ПоИННКПП(ИНН, КПП) Экспорт
		Если НЕ ЗначениеЗаполнено(ИНН) Или НЕ ЗначениеЗаполнено(КПП) Тогда
			Возврат Неопределено;
		КонецЕсли;
		СтруктураПоиска = Новый Структура("ИНН,КПП", А1Э_Строки.Встроку(ИНН), А1Э_Строки.ВСтроку(КПП));
		Запрос = А1Э_Запросы.Запрос(
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН
		|	И Контрагенты.КПП = &КПП",
		СтруктураПоиска);
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			Возврат Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;
		Таблица = Результат.Выгрузить();
		Если Таблица.Количество() > 1 Тогда
			Сообщить("Обнаружены дубли контрагентов с ИНН " + ИНН + " и КПП " + КПП + "!");
		КонецЕсли;
		Возврат Таблица[0].Ссылка;
	КонецФункции 

	Функция ПолноеНаименованиеНаДату(СсылкаИлиОбъект, Дата) Экспорт
		ИсторияНаименований = СсылкаИлиОбъект.ИсторияНаименований;
		Сч = ИсторияНаименований.Количество();
		Если Сч = 0 Тогда Возврат СсылкаИлиОбъект.НаименованиеПолное; КонецЕсли; 
		Пока Сч > 0 Цикл
			Сч = Сч - 1;
			Строка = ИсторияНаименований[Сч];
			Если Строка.Период <= Дата Тогда
				Возврат Строка.НаименованиеПолное;
			КонецЕсли;
		КонецЦикла;
		Возврат Неопределено;
	КонецФункции
	
#КонецЕсли

// Возвращает полное наименование с разверткой аббревиатур ООО, АО и прочих.
//
// Параметры:
//  Данные	 - Справочник.Контрагенты,Строка - 
//  Дата - Дата, Неопределено - если передано вместе со ссылкой на контрагента, то будет использоваться имя на соответствующий период.
// 
// Возвращаемое значение:
//   - 
//
Функция ПравильноеПолноеНаименование(ДанныеНаименованияКонтрагента, Дата = Неопределено) Экспорт
	ПолноеНаименование = ПолноеНаименование(ДанныеНаименованияКонтрагента, Дата);
	
	А1Э_Строки.Подставить(ПолноеНаименование, "ООО ", "Общество с ограниченной ответственностью ");
	А1Э_Строки.Подставить(ПолноеНаименование, "АО ", "Акционерное общество ");
	А1Э_Строки.Подставить(ПолноеНаименование, "ЗАО ", "Закрытое акционерное общество ");
	
	Возврат ПолноеНаименование;
КонецФункции 
 
// Возвращает полное наименование в развернутом виде а также в свернутом виде (в скобках).
// Такой формат часто используется при печати договоров.
//
// Параметры:
//  ДанныеНаименованияКонтрагента	 - 	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция ПредставлениеДляДоговора(ДанныеНаименованияКонтрагента, Дата = Неопределено) Экспорт
	ПолноеНаименование = ПолноеНаименование(ДанныеНаименованияКонтрагента, Дата);
	
	Возврат ПравильноеПолноеНаименование(ПолноеНаименование) + " (" + ПолноеНаименование + ")"; 
КонецФункции 

#Область Служебно

Функция ПолноеНаименование(ДанныеНаименованияКонтрагента, Дата)
	Если ТипЗнч(ДанныеНаименованияКонтрагента) = Тип("Строка") Тогда
		ПолноеНаименование = ДанныеНаименованияКонтрагента;
	ИначеЕсли А1Э_Общее.РавноОдномуИз(ТипЗнч(ДанныеНаименованияКонтрагента), Тип("СправочникСсылка.Контрагенты"), Тип("СправочникОбъект.Контрагенты")) Тогда
		Если Дата = Неопределено Тогда 
			ПолноеНаименование = ДанныеНаименованияКонтрагента.НаименованиеПолное;
		Иначе
			ПолноеНаименование = А1Э_Общее.РезультатФункцииСервер(ИмяМодуля() + ".ПолноеНаименованиеНаДату", ДанныеНаименованияКонтрагента, Дата);
		КонецЕсли;	
	Иначе
		А1Э_Служебный.СлужебноеИсключение("Неверный тип параметра ДанныеНаименованияКонтрагента - ожидается Строка или Справочник.Контрагенты (ссылка или объект)");
	КонецЕсли;
	Возврат ПолноеНаименование;
КонецФункции

#КонецОбласти

Функция ИмяМодуля() Экспорт
	Возврат "А1Бух_Контрагенты";	
КонецФункции