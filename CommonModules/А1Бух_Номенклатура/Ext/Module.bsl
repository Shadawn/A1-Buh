Функция ТекстЗапросаСписка() Экспорт
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Ссылка КАК Ссылка,
	|	ТаблицаНоменклатуры.Наименование КАК Наименование,
	|	ТаблицаНоменклатуры.Код КАК Код,
	|	ТаблицаНоменклатуры.Артикул КАК Артикул,
	|	ТаблицаНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаНоменклатуры.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|	ТаблицаНоменклатуры.Комментарий КАК Комментарий,
	|	ТаблицаНоменклатуры.НомерГТД КАК НомерГТД,
	|	ТаблицаНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения
	|ИЗ
	|	Справочник.Номенклатура КАК ТаблицаНоменклатуры"
КонецФункции

Функция СтавкаНДС(Номенклатура, Период) Экспорт
	#Если Клиент Тогда
		Возврат А1Э_ОбщееСервер.РезультатФункции("А1Бух_Номенклатура.СтавкаНДС", Номенклатура, Период);
	#Иначе	
		Возврат Перечисления.СтавкиНДС.СтавкаНДС(Номенклатура.ВидСтавкиНДС, Период);
	#КонецЕсли
КонецФункции

#Если НЕ Клиент Тогда
	
	Функция УстановитьСчетаУчетаВТабличнойЧасти(Объект, ИмяТабличнойЧасти, ТолькоПустые = Ложь) Экспорт
		ДанныеОбъекта = Новый Структура("Дата,Организация,Склад");
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
		
		ТабличнаяЧасть = Объект[ИмяТабличнойЧасти];
		МассивНоменклатуры = Новый Массив;
		Для Каждого Строка Из ТабличнаяЧасть Цикл
			Если ТолькоПустые И ЗначениеЗаполнено(Строка.Счет) Тогда Продолжить; КонецЕсли;
			МассивНоменклатуры.Добавить(Строка.Номенклатура);
		КонецЦикла;
		Если МассивНоменклатуры.Количество() = 0 Тогда Возврат Неопределено; КонецЕсли;
		ДанныеНоменклатуры = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(МассивНоменклатуры,ДанныеОбъекта, Истина);
		Для Каждого Строка Из ТабличнаяЧасть Цикл
			Если ТолькоПустые И ЗначениеЗаполнено(Строка.Счет) Тогда Продолжить; КонецЕсли;
			Строка.Счет = ДанныеНоменклатуры[Строка.Номенклатура].СчетаУчета.СчетУчета;
		КонецЦикла;
	КонецФункции 
	
#КонецЕсли

