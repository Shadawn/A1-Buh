Функция НастройкиМеханизма() Экспорт
	Настройки = А1Э_Механизмы.НовыйНастройкиМеханизма();
	
	Настройки.Обработчики.Вставить("ОбработкаЗаполнения", Истина);
	
	Настройки.ПорядокВыполнения = 1000000; //После всех прикладных обработчиков.
	
	Возврат Настройки;
КонецФункции

#Если НЕ Клиент Тогда
	
// Заполнение по умолчанию. Пытается разумно заполнить все те реквизиты (большей частью бух), для которых не должно быть дела прикладным разработчикам.
// Никогда не должна перезаполнять то, что уже заполнено. Никогда не должна падать с ошибкой - лучше что-то пропустить чем если доумент перестанет открываться.
//
// Параметры:
//  Объект				 - 	 - 
//  ДанныеЗаполнения	 - 	 - 
//  ТекстЗаполнения		 - 	 - 
//  СтандартнаяОбработка - 	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция ОбработкаЗаполнения(Объект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	Попытка
		ЗаполнитьСпособУчетаНДСПоУмолчанию(Объект);
	Исключение
	КонецПопытки;
	Попытка
		ЗаполнитьНоменклатурныеГруппыПродукцииПоУмолчанию(Объект);
	Исключение
	КонецПопытки;
	Попытка
		ЗаполнитьНоменклатурныеГруппыМатериаловПоПродукции(Объект);
	Исключение
	КонецПопытки;
	Попытка
		УстановитьСчетаУчета(Объект);
	Исключение
	КонецПопытки;
КонецФункции

Функция ЗаполнитьСпособУчетаНДСПоУмолчанию(Объект) Экспорт
	Для Каждого Строка Из Объект.Материалы Цикл
		Если НЕ ЗначениеЗаполнено(Строка.СпособУчетаНДС) Тогда
			Строка.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету;
		КонецЕсли;
	КонецЦикла;
КонецФункции

Функция ЗаполнитьНоменклатурныеГруппыПродукцииПоУмолчанию(Объект) Экспорт
	Для Каждого Строка Из Объект.Продукция Цикл
		Если НЕ ЗначениеЗаполнено(Строка.НоменклатурнаяГруппа) Тогда
			Строка.НоменклатурнаяГруппа = Строка.Номенклатура.НоменклатурнаяГруппа;
		КонецЕсли;
	КонецЦикла;
КонецФункции

Функция ЗаполнитьНоменклатурныеГруппыМатериаловПоПродукции(Объект) Экспорт
	ЕдинственнаяНоменклатурнаяГруппа = Неопределено;
	Для Каждого Строка Из Объект.Продукция Цикл
		Если ЕдинственнаяНоменклатурнаяГруппа <> Строка.НоменклатурнаяГруппа Тогда
			Если ЕдинственнаяНоменклатурнаяГруппа = Неопределено Тогда
				ЕдинственнаяНоменклатурнаяГруппа = Строка.НоменклатурнаяГруппа;
			Иначе
				Возврат Неопределено; //Если в продукции несколько номенклатурных групп, автозаполнение для материалов невозможно.
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Строка Из Объект.Материалы Цикл
		Если НЕ ЗначениеЗаполнено(Строка.НоменклатурнаяГруппа) Тогда
			Строка.НоменклатурнаяГруппа = ЕдинственнаяНоменклатурнаяГруппа;
		КонецЕсли;
	КонецЦикла;
КонецФункции 

Функция УстановитьСчетаУчета(Объект) Экспорт
	А1Бух_Номенклатура.УстановитьСчетаУчетаВТабличнойЧасти(Объект, "Материалы", Истина);
	А1Бух_Номенклатура.УстановитьСчетаУчетаВТабличнойЧасти(Объект, "Продукция", Истина);
КонецФункции 

#КонецЕсли
 
